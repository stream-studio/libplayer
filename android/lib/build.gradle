apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
android {
    compileSdkVersion 32
    namespace 'studio.stream.libplayer'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"


        externalNativeBuild {
            ndkBuild {
                def gstRoot

                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot
                else
                    gstRoot = System.env.GSTREAMER_ROOT_ANDROID

                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')

                arguments "NDK_APPLICATION_MK=jni/Application.mk", "GSTREAMER_JAVA_SRC_DIR=java", "GSTREAMER_ROOT_ANDROID=$gstRoot", "GSTREAMER_ASSETS_DIR=assets"

                targets "player_android"
                abiFilters  'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        consumerProguardFiles 'lib-proguard-rules.txt'

    }

    buildTypes {
        release{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            sourceSets {
                all {
                    manifest.srcFile 'AndroidManifest.xml'
                    java.srcDirs = ['java']
                    assets.srcDirs = ['assets']
                }
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }
}

afterEvaluate {
    if (project.hasProperty('compileDebugJavaWithJavac'))
        project.compileDebugJavaWithJavac.dependsOn 'externalNativeBuildDebug'
    if (project.hasProperty('compileReleaseJavaWithJavac'))
        project.compileReleaseJavaWithJavac.dependsOn 'externalNativeBuildRelease'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId 'stream.studio'                
                artifactId 'libplayer'
                version '0.0.1'
                artifact(bundleReleaseAar)
            }
            
            debug(MavenPublication) {
                groupId 'stream.studio'
                artifactId 'libplayer'
                version '0.0.1-debug'
                artifact(bundleDebugAar)
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                
                url = uri("https://maven.pkg.github.com/stream-studio/libplayer")

                def github_username
                def github_password

                if (project.hasProperty('github_username')){
                    github_username = project.github_username
                    github_password = project.github_password
                }
                else{
                    github_username = System.env.GITHUB_USERNAME
                    github_password = System.env.GITHUB_PASSWORD
                }

                credentials {
                    username = github_username
                    password = github_password
                }
            }
        }
    }
}

dependencies {
}